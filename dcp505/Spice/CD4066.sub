* CD4066 Analog Switch
* SYM=CD4066
* Transistor models are from LTspice group member kcin_melnick
* See message number 16897, http://tech.groups.yahoo.com/group/LTspice/
* Analog Switch Control In Out Vdd Vss
.SUBCKT CD4066 2 11 4 10 7
X1 2 6 10 7 INVERT
X2 6 1 10 7 INVERT
M1 14 6 7 7 CD4007N
M7 11 6 14 10 CD4007P
M3 11 1 14 14 CD4007N
M4 11 1 4 14 CD4007N
M8 11 6 4 10 CD4007P
.SUBCKT INVERT 1 2 3 4
* Inverter In Out Vcc Vss
M1 2 1 3 3 CD4007P
M2 2 1 4 4 CD4007N
.ENDS
.MODEL CD4007N NMOS (
+ LEVEL=1 VTO=1.44 KP=320u L=10u W=30u GAMMA=0 PHI=.6 LAMBDA=10m
+ RD=23.2 RS=90.1 IS=16.64p CBD=2.0p CBS=2.0p CGSO=0.1p CGDO=0.1p
+ PB=.8 TOX=1200n)

.MODEL CD4007P PMOS (
+ LEVEL=1 VTO=-1.2 KP=110u L=10U W=60U GAMMA=0 PHI=.6 LAMBDA=40m
+ RD=21.2 RS=62.2 IS=16.64P CBD=4.0P CBS=4.0P CGSO=0.2P CGDO=0.2P
+ PB=.8 TOX=1200N)
.ENDS
